#!/usr/bin/env perl

# THIS SCRIPT IS NOT INTENDED FOR END USERS OR FOR PEOPLE INSTALLING
# THE MODULES, BUT FOR THE AUTHOR'S USE WHEN UPDATING THE DATA FROM OFCOM'S
# PUBLISHED DATA.

use strict;
use warnings;
use Data::Dumper;
use File::Find::Rule;

$Data::Dumper::Indent = 2;
$Data::Dumper::Sortkeys = 1;

use lib 'lib';
use Number::Phone::NANP::Data;

$| = 1;

use XML::XPath;
my $xml = XML::XPath->new(filename => 'libphonenumber/resources/PhoneNumberMetadata.xml');
my @territories = $xml->find('/phoneNumberMetadata/territories/territory')->get_nodelist();

open(my $manifest_fh, 'MANIFEST') || die("Can't read MANIFEST\n");
my @manifest_files = grep { /./ } <$manifest_fh>;
close($manifest_fh);
open($manifest_fh, '>', 'MANIFEST') || die("Can't write MANIFEST\n");
print $manifest_fh "$_" foreach( grep { $_ !~ m{^lib/Number/Phone/StubCountry/} } @manifest_files);

mkdir('lib/Number/Phone/StubCountry');
mkdir('t/lib');
mkdir('t/lib/Number');
mkdir('t/lib/Number/Phone');
my $dir = 'lib/Number/Phone/StubCountry';

my @us_formats = formats_for($xml->find(
    '/phoneNumberMetadata/territories/territory[@id="US"]')->get_node(1)
);

my @gb_formats = formats_for($xml->find(
    '/phoneNumberMetadata/territories/territory[@id="GB"]')->get_node(1)
);

TERRITORY: foreach my $territory (@territories) {
  my $IDD_country_code = ''.$territory->find('@countryCode');
  my $national_code = ''.$territory->find('@nationalPrefixForParsing') || ''.$territory->find('@nationalPrefix');
  my $national_prefix_transform_rule = ''.$territory->find('@nationalPrefixTransformRule');

  my $ISO_country_code = ''.$territory->find('@id');
  if($ISO_country_code !~ /^..$/) {
    warn("skipping 'country' $ISO_country_code (+$IDD_country_code)\n");
    next TERRITORY;
  }

  unlink "$dir/$ISO_country_code.pm"; # to clear out countries that no longer exist

  open(my $module_fh, '>:encoding(UTF-8)', "$dir/$ISO_country_code.pm")
    || die("Can't write $dir/$ISO_country_code.pm: $!\n");
  print $manifest_fh "$dir/$ISO_country_code.pm\n";
  print $module_fh preamble($ISO_country_code);

  my @formatters = formats_for($territory, $ISO_country_code);
  print $module_fh 'my '.Data::Dumper->new([\@formatters], [qw(formatters)])->Dump()."\n";
  my $validators = {
      specialrate => join('|', grep { $_ ne '()' } map {
          '('.$territory->find("$_/nationalNumberPattern")->string_value().')'
      } qw(sharedCost premiumRate uan)),
      map {
          (my $xpath = $_) =~ s/_(\w)/uc($1)/eg;
          ($_ => $territory->find("$xpath/nationalNumberPattern")->string_value());
      } qw(fixed_line mobile pager voip toll_free personal_number)
  };

  $validators->{geographic} = $validators->{fixed_line};
  if($IDD_country_code == 1) {
      # see if build-nanp detected dodgy data
      if(!exists($Number::Phone::NANP::Data::fixed_line_regexes{$ISO_country_code})) {
          $validators->{geographic} = '('.$validators->{fixed_line}.'|'.$validators->{mobile}.')';
          delete $validators->{fixed_line};
          delete $validators->{mobile};
      }
  }
  # Similarly to +1, don't pass is_mobile or is_fixed_line if could be either
  if ($validators->{fixed_line} && $validators->{fixed_line} eq $validators->{mobile}) {
      delete $validators->{fixed_line};
      delete $validators->{mobile};
  }
  print $module_fh 'my '.Data::Dumper->new([$validators], [qw(validators)])->Dump();

  my $got_area_names = 0;
  CODEFILE: foreach my $codesfile (File::Find::Rule->file()->name("$IDD_country_code.txt")->in("libphonenumber/resources/geocoding")) {
    $codesfile =~ /\/(..)\/$IDD_country_code.txt$/;
    my $language = $1 || next CODEFILE;

    if($IDD_country_code == 1) {
      print $module_fh "use Number::Phone::NANP::Data;\nsub areaname {\n";
      if($ISO_country_code !~ /^(US|CA|JM)$/) {
          # we have explicit tests for US and CA areaname, and use JM to
          # check it DTRT when there's no data
          print $module_fh "# uncoverable subroutine - no data for most NANP countries
                            # uncoverable statement\n";
      }
      print $module_fh "Number::Phone::NANP::Data::_areaname('1'.shift()->{number}); }\n";
    } elsif(-e $codesfile) {
      open(my $geocoding, '<:encoding(UTF-8)', $codesfile) || die("Can't read $codesfile\n");
      my @lines = <$geocoding>;
      print $module_fh "my %areanames = ();\n" unless($got_area_names);
      $got_area_names = 1;
      foreach my $line (@lines) {
        chomp($line);
        next if($line =~ /^(#.*|\s+)?$/);
        my($prefix, $area) = split(/\|/, $line);
        $area = quotemeta($area);
        print $module_fh "\$areanames{$language}->{$prefix} = \"$area\";\n";
      }
    }
  }

  print $module_fh "
    sub new {
      my \$class = shift;
      my \$number = shift;
      \$number =~ s/(^\\+$IDD_country_code|\\D)//g;
      my \$self = bless({ number => \$number, formatters => \$formatters, validators => \$validators, ".
      (($IDD_country_code != 1 && $got_area_names) ? 'areanames => \\%areanames' : '')
      ."}, \$class);
  ";

  if ($national_code ne '' && $IDD_country_code ne '1') {
    $national_code =~ s/\s+//g;
    # Suppressing the warning below is for if the national code optionally
    # matches part of the number that would then be in the transform rule
    my @code = "return \$self if (\$self->is_valid());";

    if ($national_prefix_transform_rule eq '') {
      # There is no national prefix transform rule, so just delete the national
      # prefix, if present.
      push @code, "  \$number =~ s/^(?:$national_code)//;";
    }
    else {
      # There is a national prefix transform rule.
      push @code,
        "  my \$prefix = qr/^(?:$national_code)/;",
        "  my \@matches = \$number =~ /\$prefix/;",
        "  if (defined \$matches[-1]) {",
        # We have a match in the last capturing group.  Assume that the
        # transform rule can be applied.
        "    no warnings 'uninitialized';",
        "    \$number =~ s/\$prefix/$national_prefix_transform_rule/;",
        "  }",
        "  else {",
        # We don't have a match in the last capturing group.  Assume that the
        # transform rule cannot be applied, and instead, just delete the
        # national prefix, if present.
        "    \$number =~ s/\$prefix//;",
        "  }";
    }

    push @code,
      "  \$self = bless({ number => \$number, formatters => \$formatters, validators => \$validators, ".
      (($IDD_country_code != 1 && $got_area_names) ? 'areanames => \\%areanames' : '')
      ."}, \$class);";

    print $module_fh join "\n", map {"    $_"} @code;
    print $module_fh "\n";
  }

  print $module_fh "      return \$self->is_valid() ? \$self : undef;\n";
  print $module_fh "    }\n1;";
}

sub preamble {
  my $iso_code = shift;
  "# automatically generated file, don't edit\n\n".
  q{

# Copyright 2011 David Cantrell, derived from data from libphonenumber
# http://code.google.com/p/libphonenumber/
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
package Number::Phone::StubCountry::}.$iso_code.q{;
use base qw(Number::Phone::StubCountry);

use strict;
use warnings;
use utf8;
}.
  "our \$VERSION = 1.".join("", (gmtime())[5] + 1900, sprintf('%02d', (gmtime())[4] + 1), map { sprintf('%02d', $_) } (gmtime())[3, 2, 1, 0]).";\n\n";
}

sub formats_for {
  my ($territory, $ccode) = @_;

  return @us_formats if(
    $ccode && $ccode ne 'US' && ''.$territory->find('@countryCode') eq '1'
  );

  return @gb_formats if(
    $ccode && $ccode =~ /^(IM|JE|GG)$/
  );

  my $national_code = ''.$territory->find('@nationalPrefix');
  my $territory_national_rule = ''.$territory->find('@nationalPrefixFormattingRule');

  my @number_formats = $territory->find('availableFormats/numberFormat')->get_nodelist();

  my @formatters = ();
  foreach my $number_format (@number_formats) {
    my $number_format_pattern = ''.$number_format->find('@pattern');
    # only interested in the last one,
    # see https://github.com/DrHyde/perl-modules-Number-Phone/issues/7
    my $leading_digit_pattern = ($number_format->find('leadingDigits')->get_nodelist())[-1];
    my $formatter = ''.$number_format->find('format');
    my $formatter_intl = ''.$number_format->find('intlFormat');

    my $national_rule = ''.$number_format->find('@nationalPrefixFormattingRule') || $territory_national_rule;
    $national_rule =~ s/\$NP/$national_code/;
    $national_rule =~ s/\$FG/\$1/;

    if($leading_digit_pattern) {
      ($leading_digit_pattern = $leading_digit_pattern->string_value()); # =~ s/\s//g;
    }
    push @formatters, {
      $leading_digit_pattern ? (leading_digits => $leading_digit_pattern) : (),
      $national_rule ? (national_rule => $national_rule) : (),
      format => $formatter,
      $formatter_intl ? (intl_format => $formatter_intl) : (),
      pattern => $number_format_pattern
    };
  }
  return @formatters;

}
